/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later
 * See the lgpl.txt file in the root directory or http://www.gnu.org/licenses/lgpl-2.1.html
 */

apply from: rootProject.file( 'gradle/base-information.gradle' )

apply plugin: 'maven-publish'

if ( project.isSnapshot ) {
	// For SNAPSHOT builds all we need to define is to add the JBoss SNAPSHOT server as
	// a publishing repository - NOTE that the plugin applies security over the
	// JBoss SNAPSHOT repo via a local user's Maven `settings.xml`

	apply plugin: 'maven-publish-auth'

	publishing {
		repositories {
			maven {
				name 'jboss-snapshots-repository'
				url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
			}
		}
	}
}
else {
	// otherwise, set up the bintray plugin for publishing the release artifacts to Bintray

	apply plugin: 'com.jfrog.bintray'

	ext {
		bintrayUser = project.property( 'PERSONAL_BINTRAY_USER' )
		bintrayKey = project.property( 'PERSONAL_BINTRAY_API_KEY' )
	}

	bintray {
		user = project.bintrayUser
		key = project.bintrayKey

		repo = 'artifacts'

		pkg {
			userOrg = 'hibernate'
			name = 'hibernate-orm'

			version {
				name = project.version
				released  = new Date()
				vcsTag = project.version
				gpg {
					sign = true
				}
				attributes = [
					'jpa': '2.2',
					'family': project.hibernateMajorMinorVersion
				]
			}
		}

		publish = false
	}

}